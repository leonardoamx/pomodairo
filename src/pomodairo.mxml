<?xml version="1.0" encoding="utf-8"?>
<mx:WindowedApplication xmlns:mx="http://www.adobe.com/2006/mxml" 
	layout="absolute" height="165" width="320" showFlexChrome="false"
	verticalScrollPolicy="off" horizontalScrollPolicy="off"
	initialize="beforeInit()"
	creationComplete="init()" xmlns:views="com.views.*" xmlns:ns1="com.pomodairo.components.*"
	backgroundColor="#292929"
	closing="beforeClose()"
	title="{timerCanvas.appTitle}"
	close="shutdown()">
	
	<mx:Style source="style/style.css"/>
	
    <mx:Script>
        <![CDATA[
			import air.update.ApplicationUpdaterUI;
			import air.update.events.UpdateEvent;
			
			import com.pomodairo.Pomodoro;
			import com.pomodairo.PomodoroEventDispatcher;
			import com.pomodairo.TaskManager;
			import com.pomodairo.components.PomoItemInput;
			import com.pomodairo.components.StatisticsWindow;
			import com.pomodairo.components.config.AboutPanel;
			import com.pomodairo.components.config.AdvancedConfigPanel;
			import com.pomodairo.components.config.ConfigPanel;
			import com.pomodairo.components.config.SoundConfigPanel;
			import com.pomodairo.components.config.TimingConfigPanel;
			import com.pomodairo.components.config.TwitterConfigPanel;
			import com.pomodairo.components.timer;
			import com.pomodairo.db.Storage;
			import com.pomodairo.events.ConfigurationUpdatedEvent;
			import com.pomodairo.events.GuiChangeEvent;
			import com.pomodairo.events.PomodoroEvent;
			import com.pomodairo.twitter.TwitterManager;
			
			import mx.controls.Alert;
			import mx.controls.TextInput;
			import mx.events.MenuEvent;
			import mx.managers.PopUpManager;
        
			private var appUpdater:ApplicationUpdaterUI = new ApplicationUpdaterUI();
			
			private var taskManager:TaskManager = TaskManager.instance;
			
			private var twitterManager:TwitterManager = new TwitterManager();
			
			private function beforeInit():void {
				PomodoroEventDispatcher.getInstance().addEventListener(ConfigurationUpdatedEvent.UPDATED, onConfigurationChange);
				
				var remember:Boolean = Storage.instance.config[ConfigPanel.REMEMBER_POSITION] == "true";
				if (remember)
				{
					var setX:Number = Storage.instance.config["window.x"];
					var setY:Number = Storage.instance.config["window.y"];
					if (setX >= 0 && setX < Capabilities.screenResolutionX) {
						this.nativeWindow.x = setX;
					}
					if (setY >= 0 && setY < Capabilities.screenResolutionY) {
						this.nativeWindow.y = setY;
					}
				}
				
				// Fugly hack... loads and presets all configuration values
				var cfg:ConfigPanel = new ConfigPanel();
				cfg.populate();
				cfg.notifyConfiguration();
				
				var advancedConfig:AdvancedConfigPanel = new AdvancedConfigPanel();
				advancedConfig.populate();
				advancedConfig.notifyConfiguration();
				
				var soundConfig:SoundConfigPanel = new SoundConfigPanel();
				soundConfig.populate();
				soundConfig.notifyConfiguration();
				
				var timingConfig:TimingConfigPanel = new TimingConfigPanel();
				timingConfig.populate();
				timingConfig.notifyConfiguration();
			}
				
        	private function init():void {
        		checkForUpdates();
        		PomodoroEventDispatcher.getInstance().addEventListener(PomodoroEvent.START_POMODORO, closeTaskList);
        		PomodoroEventDispatcher.getInstance().addEventListener(PomodoroEvent.SELECTED, onPomodoroSelected);
        		PomodoroEventDispatcher.getInstance().addEventListener(PomodoroEvent.TIME_OUT, onPomodoroTimeout);
				PomodoroEventDispatcher.getInstance().addEventListener(GuiChangeEvent.UPDATED, onGuiChange);
				
				// Dispatch Twitter config after initialization to avoid locking up in a fugly graphic mode
				var tcfg:TwitterConfigPanel = new TwitterConfigPanel();
				tcfg.populate();
				tcfg.notifyConfiguration();
				
				// Display task list on startup
				toggleTaskList();
        	}
        	
        	private function checkForUpdates():void {
				appUpdater.configurationFile = new File("app:/updateConfig.xml");
				appUpdater.isCheckForUpdateVisible = false;
				appUpdater.addEventListener(UpdateEvent.INITIALIZED,
				updaterInitialised);
				appUpdater.initialize();
			}
				
			private function updaterInitialised(event:UpdateEvent):void {
				appUpdater.checkNow();
			}
			
			private function onConfigurationChange(e:ConfigurationUpdatedEvent):void {
        		trace("Configuration change: "+e.configElement.name +" -> "+e.configElement.value);
				if (e.configElement.name == ConfigPanel.ALWAYS_ON_TOP) 
				{
					this.alwaysInFront = e.configElement.value == "true";
				}
			}
			
			private function shutdown():void
			{
				twitterManager.stop();
			}
        	
        	private function onPomodoroTimeout(event:PomodoroEvent):void {
        		nativeWindow.notifyUser(NotificationType.CRITICAL);
        		nativeWindow.orderToFront();
        	}
        	
        	private function onPomodoroSelected(event:PomodoroEvent):void {
        		trace("Pomodoro selected: "+event.pomodoro.name);
        		unplannedButton.enabled = true;
        		interruptionButton.enabled = true;
        	}
        
            private function toggleTaskList():void {
            	if (!taskListPanel.visible) {
            		this.height = this.height+taskListPanel.height;
					taskListPanel.visible = true;	
            	} else {
            		closeTaskList();
            	}
            }
            
            private function closeTaskList(event:PomodoroEvent=null):void {
            	this.height = 165;
            	taskListPanel.visible = false;
            }

            private function newInterruption():void {
            	var input:PomoItemInput = new PomoItemInput();
            	input.type = Pomodoro.TYPE_INTERRUPTION;
            	input.reference = taskManager.activeTask;
            	PopUpManager.addPopUp(input, this, true);
                PopUpManager.centerPopUp(input);
            }
            
             private function newUnplanned():void {
            	var input:PomoItemInput = new PomoItemInput();
            	input.type = Pomodoro.TYPE_UNPLANNED;
            	input.reference = taskManager.activeTask;
            	PopUpManager.addPopUp(input, this, true);
                PopUpManager.centerPopUp(input);
            }
            
            private function beforeClose():void {
            	Storage.instance.setConfigurationValue("window.x",""+this.nativeWindow.x);
            	Storage.instance.setConfigurationValue("window.y",""+this.nativeWindow.y);
            }
            
			private function onGuiChange(event:GuiChangeEvent):void {
				if (event.miniView) {
					trace("Switch application to mini view");
					myMenuBar.enabled = false;
					closeTaskList();
					this.height = 52;
				} else {
					trace("Switch application to regular view");
					myMenuBar.enabled = true;
					this.height = 165;
				}			
			}
            
			private function toggleViewState():void {
				timerCanvas.toggleMiniView();
			}
			
    		/* ----------------------------------------------------
        		MENU STUFF
	   	  	---------------------------------------------------- */
	   	  
            private function menuClick(event:MenuEvent):void {
                if (event.item.@action == 'config') {
                	showSettingsDialog();
                } else if (event.item.@action == 'statistics') {
                	showStatisticsPanel();
                } else if (event.item.@action == 'twitter') {
                	showTwitterConfigPanel();
                } else if (event.item.@action == 'advancedConfig') {
                	showAdvancedSettingsDialog();
                } else if (event.item.@action == 'sounds') {
					showSoundsSettingsDialog();
				} else if (event.item.@action == 'timing') {
					showTimingSettingsDialog();
				} else if (event.item.@action == 'about') {
					showAboutPage();
				}
            }
            
            private function showSettingsDialog():void {
            	var dialog:ConfigPanel = new ConfigPanel();
            	PopUpManager.addPopUp(dialog, this, true);
                PopUpManager.bringToFront(dialog);
            }
            
            private function showTwitterConfigPanel():void {
            	var dialog:TwitterConfigPanel = new TwitterConfigPanel();
            	PopUpManager.addPopUp(dialog, this, true);
                PopUpManager.bringToFront(dialog);
            }
            
            private function showStatisticsPanel():void
	        {
            	var myWindow:StatisticsWindow = new StatisticsWindow();
		      	myWindow.systemChrome = NativeWindowSystemChrome.STANDARD;
		      	myWindow.type = NativeWindowType.NORMAL;
		      	myWindow.open( true );
	        }   
	        
	        private function showAdvancedSettingsDialog():void {
            	var dialog:AdvancedConfigPanel = new AdvancedConfigPanel();
            	PopUpManager.addPopUp(dialog, this, true);
                PopUpManager.bringToFront(dialog);
            }
			
			private function showSoundsSettingsDialog():void {
				var dialog:SoundConfigPanel = new SoundConfigPanel();
				PopUpManager.addPopUp(dialog, this, true);
				PopUpManager.bringToFront(dialog);
			}
			
			private function showTimingSettingsDialog():void {
				var dialog:TimingConfigPanel = new TimingConfigPanel();
				PopUpManager.addPopUp(dialog, this, true);
				PopUpManager.bringToFront(dialog);
			}
	        
			private function showAboutPage():void {
				var dialog:AboutPanel = new AboutPanel();
				PopUpManager.addPopUp(dialog, this, true);
				PopUpManager.bringToFront(dialog);
			}
			
			/* ----------------------------------------------------
    			END OF MENU STUFF
	   	  	---------------------------------------------------- */
	        
        ]]>
    </mx:Script>



	<mx:Fade id="fadein" />
	<mx:Fade id="fadeout" />

	
	<mx:Panel x="0" y="0" height="164" width="320" layout="absolute" mouseDown="{nativeWindow.startMove()}">				
		<ns1:timer id="timerCanvas" x="0" y="0" pomodoroLengthInMinutes="25">
		</ns1:timer>
		
		<mx:ControlBar id="controls" height="42" y="88">
			<mx:Button label="Task List" click="toggleTaskList()" toolTip="Open/close task list"/>
			<mx:Button label="Unplanned" id="unplannedButton" click="newUnplanned()" toolTip="Add unplanned" enabled="false"/>
			<mx:Button label="Interruption" id="interruptionButton" click="newInterruption()" toolTip="Add interruption" enabled="false"/>
		</mx:ControlBar>
	</mx:Panel>
	
	<mx:Label text="x" click="close()" color="#FFFFFF" x="300" y="2"/>
	<mx:Label text="_" click="minimize()" color="#FFFFFF" x="280" y="2"/>
	
	<mx:Canvas id="taskListPanel" x="0" y="166" height="300" width="320" visible="false" showEffect="fadein" hideEffect="fadeout">				
		<ns1:TaskListCanvas x="0" y="0" height="100%" width="100%">
		</ns1:TaskListCanvas>
	</mx:Canvas>
	
	<mx:MenuBar id="myMenuBar" labelField="@label" itemClick="menuClick(event)">
        <mx:XMLList>
            <menuitem label="Menu">
				<menuitem label="Config">
					<menuitem label="Configuration" action="config"/>
					<menuitem label="Sounds" action="sounds"/>
					<menuitem label="Timing" action="timing"/>
					<menuitem label="Twitter" action="twitter"/>
					<menuitem label="Advanced" action="advancedConfig"/>
				</menuitem>
				<menuitem label="Statistics" action="statistics"/>
				<menuitem label="Pomodairo" action="about"/>
            </menuitem>

        </mx:XMLList>
    </mx:MenuBar>
	
	<mx:Button x="218" y="1" height="22" width="54" id="toggleViewButton" label="view" click="toggleViewState()"/>
	
</mx:WindowedApplication>
